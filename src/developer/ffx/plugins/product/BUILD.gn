# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")
import("//src/developer/ffx/build/ffx_tool.gni")
_subtools = [
  "create",
  "download",
  "get_artifacts",
  "get_image_path",
  "get_repository",
  "get_version",
  "list",
  "lookup",
  "show",
]

rustc_library("lib") {
  name = "ffx_product"
  version = "0.1.0"
  edition = "2021"

  deps = [
    "//src/developer/ffx/lib/fho:lib",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:async-trait",
  ]
  foreach(subtool, _subtools) {
    deps += [ "$subtool:lib" ]
  }
  sources = [
    "src/args.rs",
    "src/lib.rs",
  ]
}

# When used in the build, this separate subtool version is used to avoid
# its build steps depending on all of ffx. In order to use it with ffx_action,
# you need to tell it to use the versioned executable target as follows:
#
#   ffx_tool =
#       "//src/developer/ffx/plugins/product:ffx_product_tool"
#   ffx_tool_output_name = "ffx-product"
ffx_tool("ffx_product_tool") {
  edition = "2021"
  output_name = "ffx-product"
  sources = [ "src/main.rs" ]

  deps = [
    ":lib",
    "//src/developer/ffx/lib/fho:lib",
    "//src/lib/fuchsia-async",
  ]

  sdk_target_name = "sdk"
  sdk_category = "partner"

  # Avoid instrumenting this target as it has a large negative impact on performance.
  exclude_toolchain_tags = [
    "asan",
    "coverage",
    "profile",
  ]
}

group("product") {
  public_deps = [
    ":ffx_product_tool",
    ":ffx_product_tool_host_tool",
  ]
}

group("bin") {
  public_deps = [ ":ffx_product_tool_versioned" ]
}

group("tests") {
  testonly = true
  deps = [ "common:tests" ]
  foreach(subtool, _subtools) {
    deps += [ "$subtool:lib_test($host_toolchain)" ]
  }
}
